plugins {
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.14'
  id "com.jfrog.bintray" version "1.8.5"
  id "com.jfrog.artifactory" version "4.15.2"
  id "com.github.kt3k.coveralls" version "2.10.1"
  id "org.asciidoctor.jvm.convert" version "2.4.0"
  id "org.ajoberstar.grgit" version "4.0.2"
  id "org.ajoberstar.git-publish" version "3.0.0-rc.1"
  id "org.sonarqube" version "2.8"
}

sonarqube {
  properties {
    property "sonar.projectKey", "springfox_springfox"
    property "sonar.organization", "springfox"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/ide.gradle"
apply plugin: 'springfox-multi-release'

allprojects {
  repositories {
    mavenLocal()
    maven {url "https://plugins.gradle.org/m2/"}
    maven {url "https://mvnrepository.com/"}
    maven {url 'https://maven.aliyun.com/repository/public'}
    maven {url 'https://maven.aliyun.com/repository/google'}
    maven {url 'https://maven.aliyun.com/repository/spring'}
    maven {url 'https://maven.aliyun.com/repository/jcenter'}
    maven {url 'https://maven.aliyun.com/repository/spring-plugin'}
    maven {url 'https://jitpack.io'}
    gradlePluginPortal()
  }

  ext['groovy.version'] = "4.0.0"
}

subprojects {
  apply plugin: 'java-library' //gradle groovy plugin extends the java plugin
  apply plugin: 'groovy' //gradle groovy plugin extends the java plugin
  apply plugin: 'idea'
  apply plugin: 'checkstyle'

  javadoc {
    options.encoding = 'UTF-8'
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
    options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-parameters"]
  }

  if (springfox.gradlebuild.utils.ProjectDefinitions.publishable(it)) {
    apply from: "$rootDir/gradle/publishing.gradle"
  }

  checkstyle {
    configFile = file("$rootDir/config/checkstyle.xml")
  }

  checkstyleMain.source = "src/main/java"


  group = 'io.springfox'
  version = project.rootProject.version

  task allDeps(type: DependencyReportTask) {}

  task wrapper(type: Wrapper) {
    gradleVersion = '7.4'
  }

//
//  test {
//    jvmArgs "-Dorg.spockframework.mock.ignoreByteBuddy=true"
//    useJUnitPlatform()
//  }

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

apply from: "$rootDir/gradle/documentation.gradle"

